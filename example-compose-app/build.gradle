plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def intProp(name, fallback) {
    return project.hasProperty(name) ? Integer.parseInt(project.getProperty(name)) : fallback
}

android {
    compileSdk intProp("exampleCompileSdkVersion", 35)
    namespace 'com.hcaptcha.example.compose'

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion intProp("exampleTargetSdkVersion", 35)
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
        }
    }

    lint {
        disable 'UsingOnClickInXml'
    }

    buildFeatures { // Enables Jetpack Compose for this module
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "$compose_version"
    }
}

dependencies {
    implementation project(path: ':compose-sdk')

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation 'androidx.compose.material3:material3:1.2.1'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation "androidx.compose.foundation:foundation-layout-android:$compose_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
