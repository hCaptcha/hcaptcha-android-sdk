apply plugin: 'com.android.application'
if (project.hasProperty("testingMinimizedBuild")) {
    project.setProperty("android.enableR8.fullMode", false)
    apply plugin: 'com.slack.keeper'
}

android {
    compileSdkVersion 33
    namespace 'com.hcaptcha.sdk.test'

    defaultConfig {
        applicationId "com.hcaptcha.sdk.test"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', '../sdk/consumer-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testBuildType = project.hasProperty("testingMinimizedBuild") ? "release" : "debug"
}

androidComponents {
    beforeVariants(selector().all()) { variantBuilder ->
        if (variantBuilder.name == "release") {
            variantBuilder.registerExtension(
                    com.slack.keeper.KeeperVariantMarker.class,
                    com.slack.keeper.KeeperVariantMarker.INSTANCE)
        }
    }
}

dependencies {
    implementation project(path: ':sdk')
    implementation 'androidx.appcompat:appcompat:1.6.1'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.fragment:fragment-testing:1.6.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation 'org.mockito:mockito-android:5.3.1'
}