plugins {
    id 'com.android.library'
    id 'androidx.benchmark'
}

android {
    compileSdk 34
    namespace 'com.hcaptcha.sdk.bench'

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 34

        testInstrumentationRunner 'androidx.benchmark.junit4.AndroidBenchmarkRunner'
        testInstrumentationRunnerArguments["androidx.benchmark.suppressErrors"] = "DEBUGGABLE,EMULATOR,LOW-BATTERY,UNLOCKED"
    }

    testBuildType = "release"

    buildTypes {
        debug {
            // Since debuggable can't be modified by gradle for library modules,
            // it must be done in a manifest - see src/androidTest/AndroidManifest.xml
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "benchmark-proguard-rules.pro"
        }
        release {
            isDefault = true
        }
    }
}

dependencies {
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.benchmark:benchmark-junit4:1.2.0'

    implementation project(path: ':sdk')
    implementation 'androidx.appcompat:appcompat:1.6.1'
}

// Workaround for: java.lang.ClassNotFoundException: com.android.tools.lint.client.api.Vendor
lint.enabled = false

tasks.register('pullBenchmarkData', Exec) {
    def packageName = android.namespace + ".test"

    commandLine java.nio.file.Paths.get(android.sdkDirectory.absolutePath, "platform-tools", "adb"),
            "pull",
            "/storage/emulated/0/Android/media/" \
                + "${packageName}/additional_test_output/" \
                + "${packageName.replace('.', '_')}-benchmarkData.json",
            "${projectDir}/build/outputs/"

    doFirst {
        println "pullBenchmarkData: ${commandLine.join(' ')}"
    }
}