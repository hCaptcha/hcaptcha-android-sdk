android.libraryVariants.all { variant ->
    def packageName = android.namespace
    def variantName = variant.name.capitalize()
    def outputDir = layout.buildDirectory.dir("generated/source/hcaptcha/${variant.name}/${packageName.replaceAll('\\.', '/')}").get().asFile
    def generateTask = tasks.register("generate${variantName}JavaClassFromStaticHtml") {
        group 'Generate'
        description "Generate HTML java class"

        // Declare inputs for proper incremental build support
        inputs.file("$projectDir/src/main/html/hcaptcha.html")
        inputs.file("$projectDir/src/main/html/HCaptchaHtml.java.tml")
        outputs.file("$outputDir/HCaptchaHtml.java")

        doFirst {
            def outputJavaClass = file("$outputDir/HCaptchaHtml.java")
            def template = file("$projectDir/src/main/html/HCaptchaHtml.java.tml").text
            def html = file("$projectDir/src/main/html/hcaptcha.html")
                    .readLines()
                    .stream()
                    .map({l -> "\"${l.replaceAll('"', '\\\\"')}\\n\""})
                    .collect(java.util.stream.Collectors.joining("\n${' ' * 16}+ "))

            def engine = new groovy.text.SimpleTemplateEngine()
            def src = engine.createTemplate(template).make([
                    "htmlContent": html,
                    "packageName": packageName
            ])

            outputDir.mkdirs()
            outputJavaClass.write(src.toString())
        }
    }

    // Ensure the generation task runs before compilation
    variant.registerJavaGeneratingTask(generateTask.get(), outputDir)
}