apply plugin: 'com.android.application'

def intProp(name, fallback) {
    return project.hasProperty(name) ? Integer.parseInt(project.getProperty(name)) : fallback
}

def prop(name, fallback) {
    return project.hasProperty(name) ? project.getProperty(name) : fallback
}

android {
    compileSdkVersion intProp("exampleCompileSdkVersion", 32)
    namespace 'com.hcaptcha.example'

    defaultConfig {
        applicationId "com.hcaptcha.example"
        minSdkVersion 16
        targetSdkVersion intProp("exampleTargetSdkVersion", 32)
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    compileOptions {
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
        }
    }

    lint {
        disable 'UsingOnClickInXml'
    }
}

dependencies {
    //noinspection GradleDependency
    implementation "androidx.appcompat:appcompat:${prop('exampleAppcompatVersion', '1.3.1')}"
    implementation "com.google.android.flexbox:flexbox:3.0.0"
    implementation project(path: ':sdk')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
