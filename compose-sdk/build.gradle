plugins {
    id 'maven-publish'
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "pmd"
    id "jacoco"
    id "checkstyle"
    id "com.github.spotbugs" version "5.2.3"
    id "org.owasp.dependencycheck" version "7.1.1"
    id "org.sonarqube" version "3.4.0.2513"
}

android {
    namespace 'com.hcaptcha.compose'
    compileSdk 35

    defaultConfig {
        minSdk 21

        // See https://developer.android.com/studio/publish/versioning
        // versionCode must be integer and be incremented by one for every new update
        // android system uses this to prevent downgrades
        versionCode 45

        // version number visible to the user
        // should follow semantic versioning (See https://semver.org)
        versionName "4.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildFeatures { // Enables Jetpack Compose for this module
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "$compose_version"
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api project(':sdk')
    implementation "androidx.compose.foundation:foundation:$compose_version"
}

project.afterEvaluate {
    publishing {
        repositories {
        }

        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.hcaptcha'
                artifactId = 'compose-sdk'
                version = android.defaultConfig.versionName

                pom {
                    name = 'Android Jetpack Compose SDK hCaptcha'
                    description = 'This SDK provides a wrapper for hCaptcha and ready to use Jetpack Compose Component'
                    url = 'https://github.com/hCaptcha/hcaptcha-jetpack-compose'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/hCaptcha/hcaptcha-jetpack-compose-sdk/blob/main/LICENSE'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/hCaptcha/hcaptcha-android-sdk.git'
                        developerConnection = 'scm:git:ssh://github.com:hCaptcha/hcaptcha-android-sdk.git'
                        url = 'https://github.com/hCaptcha/hcaptcha-android-sdk'
                    }
                }
            }
        }
    }
}

apply from: "$rootProject.projectDir/gradle/shared/code-quality.gradle"
